/* =====================================================================================================================
 * Docker镜像相关构建逻辑
 * =================================================================================================================== */
plugins {
	id 'base'
}

tasks.register('prepareDockerContext', Copy) { task ->
	group = 'docker'
	description = 'prepare docker context directory.'

	dependsOn 'bootJar'

	from('src/main/docker') {
		include('**/*')
	}

	from(layout.buildDirectory.dir('libs')) {
		include('**/*.jar')
		exclude('**/*-javadoc.jar', '**/*-sources.jar')
	}

	into(layout.buildDirectory.dir('dockerContext'))
}

tasks.register('buildDockerImage', Exec) { task ->
	group = 'docker'
	description = 'build docker image.'

	dependsOn 'prepareDockerContext'
	//finalizedBy 'cleanPrepareDockerContext'

	commandLine('bash', "$rootDir/buildSrc/config/docker/build-image.sh", "$dockerImageTag")
	workingDir(layout.buildDirectory.dir('dockerContext'))
}

tasks.register('removeDockerImage', Exec) { task ->
	group = 'docker'
	description = 'remove docker image.'

	finalizedBy 'cleanPrepareDockerContext'
	commandLine('bash', "$rootDir/buildSrc/config/docker/remove-image.sh", "$dockerImageTag")
}
